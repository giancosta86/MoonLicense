/*ยง
  ===========================================================================
  MoonLicense
  ===========================================================================
  Copyright (C) 2015 Gianluca Costa
  ===========================================================================
  Licensed under the Apache License, Version 2.0 (the "License");
  you may not use this file except in compliance with the License.
  You may obtain a copy of the License at

       http://www.apache.org/licenses/LICENSE-2.0

  Unless required by applicable law or agreed to in writing, software
  distributed under the License is distributed on an "AS IS" BASIS,
  WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
  See the License for the specific language governing permissions and
  limitations under the License.
  ===========================================================================
*/

plugins {
    id 'com.jfrog.bintray' version '1.1'
}


apply plugin: 'java'
apply plugin: 'maven'


group = 'info.gianlucacosta.moonlicense'
archivesBaseName = 'moonlicense'
version = '1.0'

description = 'Language-independent Java library for setting/removing license notices'

sourceCompatibility = 1.8


repositories {
    jcenter()
}

dependencies {
    testCompile group: 'junit', name: 'junit', version: '4.11'
}


task('sourcesJar', type: Jar, dependsOn: 'classes') {
    classifier = 'sources'
    from project.sourceSets.main.allSource
}


task('javadocJar', type: Jar, dependsOn: 'javadoc') {
    classifier = 'javadoc'
    from project.javadoc.destinationDir
}


artifacts {
    archives project.sourcesJar
    archives project.javadocJar
}


uploadArchives {
    repositories {
        mavenDeployer {
            repository(
                    url: "file://" + new File(project.buildDir, "mavenDeploy")
            )

            pom.project {
                name project.name
                description project.description

                inceptionYear '2015'
                url 'https://github.com/giancosta86/MoonLicense'


                licenses {
                    license {
                        name 'Apache License, Version 2.0'
                        url 'https://www.apache.org/licenses/LICENSE-2.0'
                        distribution 'repo'
                    }
                }

                developers {
                    developer {
                        name 'Gianluca Costa'
                        email 'gianluca@gianlucacosta.info'
                    }
                }

                scm {
                    url 'git@github.com:giancosta86/MoonLicense.git'
                    connection 'scm:git:git@github.com:giancosta86/MoonLicense.git'
                    developerConnection 'scm:git:git@github.com:giancosta86/MoonLicense.git'
                }

            }
        }
    }
}


bintray {
    if (project.hasProperty("bintrayKey")) {
        user = bintrayUser
        key = bintrayKey
    }


    filesSpec {
        from('build/mavenDeploy') {
            include "**/${project.version}/*.jar"
            include "**/${project.version}/*.pom"
        }
        into '.'
    }

    dryRun = false
    publish = false

    pkg {
        repo = 'Hephaestus'

        name = project.name
        desc = project.description

        websiteUrl = 'https://github.com/giancosta86/MoonLicense'
        issueTrackerUrl = 'https://github.com/giancosta86/MoonLicense/issues'
        vcsUrl = 'https://github.com/giancosta86/MoonLicense.git'

        licenses = ['Apache-2.0']
        labels = ['licenses', 'notices', 'source files', 'library', 'Java', 'language-independent', 'extensible']

        publicDownloadNumbers = false

        version {
            name = project.version
            vcsTag = 'v' + project.version

            released = new Date()

            gpg {
                sign = true
            }

            mavenCentralSync {
                sync = false
            }
        }
    }
}
